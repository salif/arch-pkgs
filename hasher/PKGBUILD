# Maintainer: Salif Mehmed <salifm@protonmail.com>

pkgname=hasher
pkgver=1.2.0
pkgrel=1
pkgdesc="Hasher"
arch=("x86_64")
url="https://salifm.github.io/hasher"
license=("MIT")
makedepends=("go")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/salifm/hasher/archive/v$pkgver.tar.gz")
sha256sums=("2b122fa60262ad8ebbbc6edae58fd8b1861a2750cc47fbff3e12e051289ba0c7")

build() {
    cd "$pkgname-$pkgver"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS} -s -w"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -o ./build/hasher ./cmd/hasher
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm755 build/hasher "$pkgdir"/usr/bin/hasher
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
